plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'net.minecraftforge.gradleutils' version '2.+'
}

group = 'net.minecraftforge'
version = gradleutils.getTagOffsetVersion()
archivesBaseName = 'groovydslimprover'

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

changelog {
    fromTag '1.0'
}

sourceSets {
    runtime
}

repositories {
    mavenCentral()
}

license {
    header = file('LICENSE-header')
    include '**/*.java'
    include '**/*.groovy'
}

dependencies {
    implementation sourceSets.runtime.output
    implementation gradleApi()
    runtimeImplementation gradleApi()
    compileOnly 'org.jetbrains:annotations:24.0.0'

    testImplementation 'org.mockito:mockito-core:5.1.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.1.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

jar {
    from sourceSets.runtime.output
    manifest.attributes([
            'Specification-Title'   : 'GroovyDSLImprover',
            'Specification-Vendor'  : 'MinecraftForge',
            'Implementation-Title'  : project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor' : 'MinecraftForge',
            'Git-Commit'            : gradleutils.gitInfo.abbreviatedId,
            'Git-Branch'            : gradleutils.gitInfo.branch,
            'Build-Number'          : "${System.getenv('BUILD_NUMBER') ?: 0}"
    ])
}

sourcesJar {
    from sourceSets.runtime.allSource
}

tasks.withType(GroovyCompile).configureEach {
    options.incremental = true
    groovyOptions.optimizationOptions.indy = true
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'groovydslimprover'

            pom {
                name = 'GroovyDSLImprover'
                description = 'Groovy Compiler Plugin to improve building Groovy based DSLs, like those used in Gradle.'
                url = 'https://github.com/MinecraftForge/GroovyDSLImprover'
                scm {
                    url = 'https://github.com/MinecraftForge/GroovyDSLImprover'
                    connection = 'scm:git:git://github.com/MinecraftForge/GroovyDSLImprover.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/GroovyDSLImprover.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/GroovyDSLImprover/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'matyrobbrt'
                        name = 'matyrobbrt'
                    }
                }
            }
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}